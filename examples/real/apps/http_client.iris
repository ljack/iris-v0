;; Example app: HTTP client using stdlib/http.
;; Demonstrates network effects and Result handling.
(program
  (module (name "http_client_example") (version 0))
  (imports
    (import "../../../stdlib/http" (as "Client"))
    (import "../../../stdlib/pretty" (as "pretty"))
  )
  (defs
    (deffn (name print_usage)
      (args)
      (ret I64)
      (eff !IO)
      (body
        (io.print "Usage: http_client.iris [--verbose|--help]")
      )
    )

    (deffn (name print_verbose_request)
      (args (url Str))
      (ret I64)
      (eff !IO)
      (body
        (let* ((_ (io.print "--- Request ---"))
               (_ (io.print (str.concat "Method: " "GET")))
               (_ (io.print (str.concat "URL: " url)))
               (_ (io.print "Headers:"))
               (_ (io.print ""))
               (_ (io.print "Body: (empty)")))
          0)
      )
    )

    (deffn (name print_verbose_response)
      (args (p (Record (version Str) (status I64) (headers (List (Record (key Str) (val Str)))) (body Str))))
      (ret I64)
      (eff !IO)
      (body
        (let* ((_ (io.print "--- Response Status ---"))
               (_ (io.print (record.get p "status")))
               (_ (io.print "--- Response Version ---"))
               (_ (io.print (record.get p "version")))
               (_ (io.print "--- Response Headers ---"))
               (_ (io.print (pretty.headers_to_str (record.get p "headers"))))
               (_ (io.print "--- Response Body ---"))
               (_ (io.print (record.get p "body"))))
          0)
      )
    )

    (deffn (name print_short_response)
      (args (p (Record (version Str) (status I64) (headers (List (Record (key Str) (val Str)))) (body Str))))
      (ret I64)
      (eff !IO)
      (body
        (let* ((_ (io.print "--- Response Status ---"))
               (_ (io.print (record.get p "status")))
               (_ (io.print "--- Response Version ---"))
               (_ (io.print (record.get p "version"))))
          0)
      )
    )

    (deffn (name run_simple)
      (args (url Str))
      (ret (Result Str Str))
      (eff !Net)
      (body
        (let (res (Client.get url))
          (match res
            (case (tag "Ok" (p))
              (let (_ (print_short_response p))
                (Ok "Success")
              )
            )
            (case (tag "Err" (e))
              (Err (str.concat "Request Failed: " e))
            )
          )
        )
      )
    )

    (deffn (name run_verbose)
      (args (url Str))
      (ret (Result Str Str))
      (eff !Net)
      (body
        (let* ((_ (print_verbose_request url))
               (res (Client.get url)))
          (match res
            (case (tag "Ok" (p))
              (let (_ (print_verbose_response p))
                (Ok "Success")
              )
            )
            (case (tag "Err" (e))
              (Err (str.concat "Request Failed: " e))
            )
          )
        )
      )
    )

    (deffn (name main)
      (args)
      (ret (Result Str Str))
      (eff !Net)
      (doc "Fetch https://google.com and print response fields. Use --verbose or --help.")
      (body 
        (let* ((args (sys.args))
               (flag_opt (list.get args 0))
               (url "https://google.com"))
          (match flag_opt
            (case (tag "Some" (v))
              (if (= v "--help")
                (let (_ (print_usage)) (Ok "Success"))
                (if (= v "--verbose")
                  (run_verbose url)
                  (let (_ (print_usage))
                    (Err (str.concat "Unknown flag: " v))
                  )
                )
              )
            )
            (case (tag "None")
              (run_simple url)
            )
          )
        )
      )
    )
  )
)
