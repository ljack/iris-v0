(program
  (module (name "typecheck") (version 2))
  (imports
    (import "ast" (as "ast"))
    (import "list" (as "list"))
    (import "str" (as "str"))
    (import "type_env" (as "env"))
    (import "type_eq" (as "teq"))
    (import "type_check_expr" (as "tce"))
  )
  (defs
    (deffn (name type_check_program) (args (p ast.Program)) (ret (Result ast.IrisType Str)) (eff !Pure)
        (body
            (let (e (call env.env_new))
            (let (e2 (call collect_defs p.progDefs e))
                (call type_check_defs p.progDefs e2)
            ))
        )
    )

    (deffn (name collect_defs) (args (defs (List ast.Definition)) (e env.TypeEnv)) (ret env.TypeEnv) (eff !Pure)
        (body
            (match defs
                (case (tag "nil") e)
                (case (tag "cons" (h t))
                    (match h
                        (case (tag "DefFn" (f)) 
                            (let (fn_ty (tag "Fn" (record (args (call tce.arg_list_to_type_list f.args)) (ret f.ret) (eff f.eff))))
                                (call collect_defs t (call env.env_put_fn e f.name fn_ty))
                            )
                        )
                        (case (tag "TypeDef" (td)) (call collect_defs t e))
                    )
                )
            )
        )
    )

    (deffn (name type_check_defs) (args (defs (List ast.Definition)) (e env.TypeEnv)) (ret (Result ast.IrisType Str)) (eff !Pure)
        (body
            (match defs
                (case (tag "nil") (tag "Ok" (tag "I64" (record))))
                (case (tag "cons" (h t))
                    (match h
                        (case (tag "DefFn" (f))
                            (let (res (call type_check_def f e))
                                (match res
                                    (case (tag "Ok" (_)) (call type_check_defs t e))
                                    (case (tag "Err" (err)) (tag "Err" err))
                                )
                            )
                        )
                        (case (tag "TypeDef" (td)) (call type_check_defs t e))
                    )
                )
            )
        )
    )

    (deffn (name type_check_def) (args (f (Record (name Str) (args (List ast.Arg)) (ret ast.IrisType) (eff Str) (body ast.Expr))) (e env.TypeEnv)) (ret (Result ast.IrisType Str)) (eff !Pure)
        (body
            (let (new_env (call env_put_args e f.args))
                (let (res (call tce.type_check f.body new_env))
                    (match res
                        (case (tag "Ok" (ty))
                            (if (call teq.type_eq ty f.ret)
                                (tag "Ok" ty)
                                (tag "Err" (str.concat "Return type mismatch in " f.name))
                            )
                        )
                        (case (tag "Err" (err)) (tag "Err" err))
                    )
                )
            )
        )
    )

    (deffn (name env_put_args) (args (e env.TypeEnv) (l (List ast.Arg))) (ret env.TypeEnv) (eff !Pure)
        (body
            (match l
                (case (tag "nil") e)
                (case (tag "cons" (h t))
                    (call env_put_args (call env.env_put e h.name h.type) t)
                )
            )
        )
    )

    (deffn (name main) (args) (ret I64) (eff !Pure)
        (body 0)
    )
  )
)
